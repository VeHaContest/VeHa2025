----MODULE 1_file----
EXTENDS Naturals

(*    ОПИСАНИЕ СПЕЦИФИКАЦИИ №1    *)

(* Мы разбили задачу разработки спецификации распределенной системы
   контроля версий на несколько простых этапов, в каждом из которых мы
   концентрируемся на одном конкретном элементе. На этом уровне мы
   формализуем понятие файла и операцию записи в него.

   В этой спецификации описывается система, состоящая из одного файла. Файл
   состоит из нескольких строк, в каждой из которых может быть записано
   некоторое число. Мы считаем, что система реализована и работает корректно,
   если после нескольких таких операций записи система переходит в некоторое
   заранее заданное состояние (см. определение ANSWER и действие Success ниже).
   Данное поведение описано в спецификации в действиях Write, Success и Next.

   В задании требуется изучить спецификацию, понять, как она работает,
   и реализовать тело оператора NewFile. Это нужно сделать таким образом,
   чтобы выполнялись следующие требования:
   - NewFile возвращает новое значение для всего файла, при этом модифицируя
     только строку, на указывает параметр line_num.
   - Новое значение вычисляется на основе старого результатом применения
     следующей арифметической операции: нужно взять значение, записанное
     в строке, и умножить его на номер строки.
   - Поведение модели должно быть детерминировано и завершаться в состоянии
     Success: в данном состоянии в файле на первой строке записано 1,
     во второй: 2048, в третьей 81. Т.е., file = <<1, 2048, 81>>.

   Проверить корректность спецификации запуском TLC. Если все в порядке,
   переходить к следующему заданию.

   Модифицировать текст спецификации можно только в местах, которые
   помечены текстом "INSERT YOU CODE HERE". Вносить изменения в прочие части
   можно только в случае, если вы нашли в приведенном коде ошибку.
 *)

\* Константы задают начальные условия моделирования,
\* они определяются в конфигурационном файле.
CONSTANTS
    \* Ограничиваем максимальное число строк в файле
    MAX_TEXT_LINES

\* Пояснение относительно констант и прочих ограничений в спецификации.
\* Дело в том, что TLC - это explicit state model-checker. То есть,
\* инструмент в явном виде генерирует все возможные состояния и
\* допустимые переходы между ними. Поэтому он подвержен так называемому
\* комбинаторному взрыву, когда рост пространства состояний экспоненциально
\* зависит от размера состояния.
\* И очень важно это всегда иметь ввиду и ограничивать спецификацию таким
\* образом, чтобы проверить все интересные свойства при минимально-возможном
\* пространстве состояний.
\* Вопрос выбора ограничений (размеров массивов, диапазонов значений и пр)
\* в общем случае нетривиален и чаще всего ограничения подбираются на основе
\* опыта, интуиции и пр.

\* file - переменная, где хранится содержимое файла.
\* Так как не интересуют детали содержимого файла,
\* то структура файла довольно простая - это ассоциативный массив,
\* где ключ - номер строки, а строка - некоторое число.
\* Ассоциативный массив можно задать в TLA+ c помощью последовательности
\* (sequence), в TLA+ они индексируются начиная с 1.
\*
\* KEY -> VALUE
\*   1 -> 2
\*   2 -> 2
\*   3 -> 1
\*
\* Данная запись соответствует следующей записи на TLA+:
\*      file = <<2, 2, 1>>
\*
\* Количество строк в файле не меняется,
\* меняется только содержимое строк.
VARIABLES
    file

\* Множество возможных номеров строк
TEXT_LINES == 1 .. MAX_TEXT_LINES
\* В начальном состоянии в каждой строке записано значение INIT_VALUE
INIT_VALUE == 1
ANSWER == <<1, 2048, 81>>

\* При записи значения в файл, создаём новую структуру файла,
\* где точечно меняем значение в заданной строке.
\* Можно было бы значения выбирать недетерминированно, но тогда
\* будет большой рост пространства состояний, что потом будет
\* весьма сильно тормозить проверку темпоральных свойств.
\* Поэтому значения будут генерироваться детерминированно
\* (по весьма простому закону), что никак не влияет на интересные
\* нам свойства, но существенно ускоряет работу TLC.
NewFile(line_num) ==
    (* VVVV     INSERT YOU CODE HERE     VVVV *)
    [file EXCEPT ![line_num] = file[line_num] * line_num]
    (* ^^^^     INSERT YOU CODE HERE     ^^^^ *)

\* Операция записи, которая сначала проверяет аргументы, а затем
\* задаёт новое значение переменной file на следующем шаге
Write(line_num) ==
    /\ line_num \in TEXT_LINES
    /\ file' = NewFile(line_num)

\* Вспомогательное действие, который описывает возможные аргументы
\* для вызова действия Write. Обычно на TLA+ такие конструкции
\* пишут в действии Next, но вынос в отдельное действие улучшает
\* визуализацию выдачи модел чекера TLC.
WriteAction ==
    \E line_num \in TEXT_LINES:
        /\ file[line_num] < ANSWER[line_num]
        /\ Write(line_num)

\* тест правильности записи в файл, некоторые поведения системы должны
\* привести к заданному содержимому файла при правильной реализации
\* функций NewFile и Write
Success ==
    /\ file = ANSWER
    /\ UNCHANGED file

\* Инициализируем переменные состояния
Init ==
    /\ file = [l \in TEXT_LINES |-> INIT_VALUE] \* начальное содержимое файла

\* Шаг моделирования: недетерминировано выбираем номер строки
\* и пишем новое значение в выбранную строку, или проверяем, что мы уже
\* достигли конечного состояния с требуемым содержимым файла.
Next ==
    \/ WriteAction
    \/ Success

\* Тип файла - множество всех возможных вариантов содержимого файла
FileType == [TEXT_LINES -> Nat]

\* Инвариант корректности типа файла
TypeOK ==
    file \in FileType

====
